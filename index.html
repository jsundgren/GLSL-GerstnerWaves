<!DOCTYPE html>
<html>
	<head>
		<meta charset=utf-8>
		<title>TNM084 - Wave simulation using Gerstner waves</title>
        <!-- Custom CSS -->
        <link href="css/style.css" rel="stylesheet">
                
        <script type="x-shader/x-vertex" id="vertexShader">
        /////// VERTEX SHADER ////////
            uniform float time;            
            float PI = 3.1456;
            float g = 9.82;
            
            
            vec3 disp_normal;
            varying vec3 vVertex_position;
            
            // wave(dirX, dirY, steepness, wavelength)
            vec4 wave_a = vec4(1.0, 0.0, 0.25, 55.0);
            vec4 wave_b = vec4(1.0, 0.4, 0.2, 28.0);
            vec4 wave_c = vec4(1.0, 1.0, 0.3, 20.0);
            
            
            // Function to calculate each Gerstner wave and accumulate the normals by recalculating the tangent and binormal
            vec3 gerstner_wave(vec4 wave, vec3 pos, inout vec3 tangent, inout vec3 binormal){
                
                float steepness = wave.z;
                float wave_length = wave.w;
                // Wave number 
                float wave_number = 2.0 * PI / wave_length;
                // Speed
                float wave_speed = sqrt(g / wave_number);
                // Direction
                vec2 dir = normalize(wave.xy);
                // Base of gerstner function
			    float f = wave_number * (dot(dir, pos.xy) - wave_speed * time);
                // Amplitude
                float amp = steepness / wave_number;
                
                // Calculate the partial derivate in the different dimensions to get correct the normals after displacement
                tangent += vec3(-dir.x * dir.x * (steepness * sin(f)),
                                dir.x * (steepness * cos(f)),
                                -dir.x * dir.y * (steepness * sin(f)));
                                
                binormal += vec3(-dir.x * dir.y * (steepness * sin(f)),
                                dir.y * (steepness * cos(f)),
                                -dir.y * dir.y * (steepness * sin(f)));
                
                vec3 g_wave = vec3(dir.x * (amp * cos(f)),	dir.y * (amp * cos(f)), amp * sin(f));
                return g_wave;
            }
            
            void main() {
              vVertex_position = position;
              vec3 new_position = position;
              vec3 tangent = vec3(1.0, 0, 0);
              vec3 binormal = vec3(0, 0, 1.0);
              vec3 wave_pos = new_position;
              wave_pos += gerstner_wave(wave_a, new_position, tangent, binormal);
              wave_pos += gerstner_wave(wave_b, new_position, tangent, binormal);
              wave_pos += gerstner_wave(wave_c, new_position, tangent, binormal);
              
              disp_normal = normalize(cross(binormal, tangent));
              gl_Position = projectionMatrix * modelViewMatrix * vec4( wave_pos, 1.0 );
              
            }
        </script>
        
        
        <script type="x-shader/x-vertex" id="fragmentShader">
        /////// FRAGMENT SHADER ////////  
            // Same calculations are done here as above to eliminate artifacts in the shading
            
            uniform vec3 camera;
            uniform vec3 light;
            
            varying vec3 vVertex_position;
            vec3 normal;
            vec3 l_color = vec3(1.0, 1.0, 1.0);
            
            uniform float time;            
            float PI = 3.1456;
            float g = 9.82;
            
            // wave(dirX, dirY, steepness, wavelength)
            vec4 wave_a = vec4(1.0, 0.0, 0.25, 55.0);
            vec4 wave_b = vec4(1.0, 0.4, 0.2, 28.0);
            vec4 wave_c = vec4(1.0, 1.3, 0.3, 20.0);
            
            
            vec3 gerstner_wave(vec4 wave, vec3 pos, inout vec3 tangent, inout vec3 binormal){
                
                float steepness = wave.z;
                float wave_length = wave.w;
                float wave_number = 2.0 * PI / wave_length;
                float wave_speed = sqrt(g / wave_number);
                vec2 dir = normalize(wave.xy);
			    float f = wave_number * (dot(dir, pos.xy) - wave_speed * time);
                float amp = steepness / wave_number;
                
                tangent += vec3(-dir.x * dir.x * (steepness * sin(f)),
                                dir.x * (steepness * cos(f)),
                                -dir.x * dir.y * (steepness * sin(f)));
                                
                binormal += vec3(-dir.x * dir.y * (steepness * sin(f)),
                                dir.y * (steepness * cos(f)),
                                -dir.y * dir.y * (steepness * sin(f)));
                
                vec3 g_wave = vec3(dir.x * (amp * cos(f)),	dir.y * (amp * cos(f)), amp * sin(f));
                return g_wave;
            }
            
            void main() {
            
              vec3 new_position = vVertex_position;
              vec3 tangent = vec3(1.0, 0, 0);
              vec3 binormal = vec3(0, 0, 1.0);
              vec3 wave_pos = new_position;
              wave_pos += gerstner_wave(wave_a, new_position, tangent, binormal);
              wave_pos += gerstner_wave(wave_b, new_position, tangent, binormal);
              wave_pos += gerstner_wave(wave_c, new_position, tangent, binormal);
              normal = normalize(cross(binormal, tangent));
            
              // Phong shading 
              vec3 N = normal;
              vec3 L = normalize( light - wave_pos );
              vec3 V = normalize( camera - wave_pos );
              vec3 R = normalize( reflect( -light, N ));

              float ambient = 0.1;
              vec3 amb = ambient * l_color;
              float diffuse = max(dot(N, L), 0.0);
              vec3 diff = diffuse * l_color;
              float spec_constant = 0.5;
              float specular = pow(max(dot(V, R), 0.0), 16.0);
              vec3 spec = specular * spec_constant * l_color;
              vec3 material = vec3( 1.0/255.0, 69.0/255.0, 178.0/255.0 );
              vec3 newColor = ( spec + material ) * ( diff + amb );
              
              gl_FragColor = vec4( newColor, 1.0 );
            }
        </script>

	</head>
	<body>
        <div id="info"> 
            <h1>GERSTNER WAVES</h1>
            <h3>Press 'W' to activate wireframe.</h3>
            
        </div>
		<script src="js/three.js"></script>
        <script src="js/OrbitControls.js"></script>
		<script src="js/scene.js"></script>
        <script src="js/geometrySetup.js"></script>
        <script src="js/render.js"></script>        
	  
    </body>
</html>